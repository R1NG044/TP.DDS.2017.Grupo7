package org.drools.devguide;

import org.drools.devguide.eshop.model.Client;
import org.drools.devguide.eshop.model.Client.ClientCategory;
import org.drools.devguide.eshop.model.ClientDiscountEvaluation;
import org.drools.devguide.sales.service.ClientService;
import org.drools.devguide.sales.service.ClientDiscountService;

import java.util.List;

global Client.ClientCategory[] discountCategories;
global ClientService clientService;
global List results;

rule "Apply no discount"
when
    String(
        this == "calculate discounts"
    )

    $c: Client(
        category not memberOf discountCategories
    ) from clientService.getAllClients()

then
    ClientDiscountEvaluation cde = new ClientDiscountEvaluation($c);
    cde.setDiscount(0.0f);
    insert(cde);
end

rule "Apply 10 percent discount"
when
    String(
        this == "calculate discounts"
    )

    $c: Client(
        category memberOf discountCategories
    ) from clientService.getAllClients()

then
    ClientDiscountEvaluation cde = new ClientDiscountEvaluation($c);
    cde.setDiscount(0.1f);
    insert(cde);
end

rule "Collect results"
when
    $cde: ClientDiscountEvaluation()
then
    results.add($cde);
end

rule "Persist results"
when
    $cde: ClientDiscountEvaluation()
then
    channels["discount-storage"].send($cde);
end


query "Get All Client Discounts"
    $cde: ClientDiscountEvaluation()
end

query "Get Client Discounts" (String $id)
    $cde: ClientDiscountEvaluation(client.id == $id)
end
