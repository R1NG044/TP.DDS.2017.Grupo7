package org.drools.devguide;

import org.drools.devguide.sales.model.Client;
import org.drools.devguide.sales.model.Client.ClientCategory;
import org.drools.devguide.sales.model.ClientDiscountEvaluation;
import org.drools.devguide.sales.service.ClientService;

global Client.ClientCategory[] discountCategories;
global ClientService clientService;

rule "Apply no discount"
when
    String(
        this == "calculate discounts"
    )

    $c: Client(
        category not memberOf discountCategories
    ) from clientService.getAllClients()

then
    ClientDiscountEvaluation cde = new ClientDiscountEvaluation($c);
    cde.setDiscount(0.0f);
    insert(cde);
end

rule "Apply 10 percent discount"
when
    String(
        this == "calculate discounts"
    )

    $c: Client(
        category memberOf discountCategories
    ) from clientService.getAllClients()

then
    ClientDiscountEvaluation cde = new ClientDiscountEvaluation($c);
    cde.setDiscount(0.1f);
    insert(cde);
end